Import("env")

env.Append(
    CPPPATH=[
        "#/lib/flipper_format",
    ],
    SDK_HEADERS=[
        File("flipper_format.h"),
        File("flipper_format_i.h"),
        File("flipper_format_stream.h"),
    ],
    LINT_SOURCES=[
        Dir("."),
    ],
)


libenv = env.Clone(FW_LIB_NAME="flipperformat")
libenv.ApplyLibFlags()

if libenv["RAM_EXEC"]:
    libenv.Append(
        CPPDEFINES=[
            "FLIPPER_STREAM_LITE",
        ],
    )


sources = libenv.GlobRecursive("*.c")

lib = libenv.StaticLibrary("${FW_LIB_NAME}", sources)
libenv.Install("${LIB_DIST_DIR}", lib)
Return("lib")
